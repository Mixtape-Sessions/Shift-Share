---
output: github_document
---

```{r setup, include = F}
# devtools::install_github("Hemken/Statamarkdown")
library(Statamarkdown)
```


# Mixtape SSIV Workshop: Coding Lab

This lab will walk through some basic SSIV analyses using data from [Autor, Dorn, and Hanson](https://github.com/Mixtape-Sessions/Shift-Share/blob/main/Readings/Autor_Dorn_Hanson_2013) (ADH, 2013). As discussed in lecture, ADH use a shift-share instrument aggregating Chinese import shocks across 397 manufacturing instruments with exposure weights calculated as the economy-wide share of (baseline) industry employment. We will use three cleaned datasets from their setup:

- `adh_shocks.dta`: an industry-by-year dataset of the shocks

- `adh_shares.dta`: a location-by-industry-by-year dataset of the shares

- `adh_noIV.dta`: a location-by-year dataset of the main outcome (manufacturing employment growth, `y`), treatment (local growth of China import exposure, `x`), and other useful variables -- excluding the ADH instrument


## Exercises:

1. Construct the ADH (location-by-year) instrument by appropriately combining the data on shocks and shares. Merge this into the `adh_noIV` dataset, and estimate an IV regression of the outcome onto the treatment which controls for year (i.e. the `post` variable) and weights by baseline total employment (the `weight` variable), clustering by `state`. Then estimate the exact same IV regression replacing the outcome `y` with the lagged outcome `y_lag`, capturing growth in manufacturing employment that took place before the ADH "China Shock" quasi-experiment. How does the latter IV regression help build support for the former IV regression?

```{stata, collectcode = T}
/*****************************************/
/*    Example Solution for SSIV Lab      */
/* Written by Peter Hull, 5/16/2022 (v1) */
/*****************************************/

* ssc install ssaggregate, replace
* clear all

/* Construct z */
use adh_shares, clear
merge m:1 industry year using adh_shocks, nogen 
gen z = ind_share * shock
collapse (sum) z, by(location year)
quietly tempfile IV
quietly save `IV', replace
use adh_noIV, clear
merge 1:1 location year using `IV', nogen

```

```{stata}
/* Basic SSIV regression */
ivreg2 y (x=z) post [aw=weight], cluster(state) 
```

```{stata}
/* Basic balance test */
ivreg2 y_lag (x=z) post [aw=weight], cluster(state) 
```

*Comments:*

The lag outcome IV estimate is much smaller than the main IV estimate (-0.131 vs -0.746) and statistically insignificant. This tells us that regions which would get a large “china shock” in the post period are not on differential outcome trends in the pre period, building support for the validity of the instrument. To do this comparison properly we should use exposure-robust standard errors, i.e. by the ssaggregate command used below. But as you’ll see below the standard errors are not too different this way.



2.  Construct the "sum-of-shares" control from the `adh_shares` dataset and add this control to both of the previous IV regressions. How does the main IV estimate change? Why, intuitively, is this control important to include?

```{stata, collectcode = T}

/* Add sum of shares control */
preserve
use adh_shares, clear
collapse (sum) sum_share=ind_share, by(location year)
tempfile sum_shares
save `sum_shares', replace
restore
merge 1:1 location year using `sum_shares', nogen
summ sum_share
```

```{stata}
/* SSIV with Sum of Shares */
ivreg2 y (x=z) post sum_share [aw=weight], cluster(state) 
```

```{stata}
/* Balance test with Sum of Shares */
ivreg2 y_lag (x=z) post sum_share [aw=weight], cluster(state) 
```

*Comments:*

The sum-of-shares control should be included because ADH is a setting with "incomplete shares" (i.e. the sum of shares I not constant across location-years). Without this control the SSIV will be using both the variation in shocks across industries and the average “size” of the shock through the sum of shares (unless the shocks are mean-zero, which you can see they are not).


3.  Interact the "sum-of-shares" control with year and add this control to both of the previous IV regressions. How do both IV estimates change? Can you see why, intuitively, the interaction control shifts the main IV estimate so much?

```{stata, collectcode = T}
/* Interact sum of shares with year */
gen post_sum_share=post*sum_share
```

```{stata}
ivreg2 y (x=z) post sum_share post_sum_share [aw=weight], cluster(state) 
```

```{stata}
ivreg2 y_lag (x=z) post sum_share post_sum_share [aw=weight], cluster(state) 
```


```{stata}
/* Check why sum of shares matters */
preserve
use adh_shocks, clear
reg shock year, cluster(industry)
restore
```

*Comments:*

Interacting the sum-of-shares control with year isolates the within-year variation in shocks. To see this take the year fixed effects as the industry-level "q_n" discussed in class and note that to leverage this control we need to control for sum_n (s_lnt*q_n) = sum_nt (s_ln)*period_t in the location-year regression. You can see that the shock mean is quite different across periods (in the post period the average shock is significantly larger) such that isolating the within-period variation makes a difference – without this control the SSIV is using both within- and across-period shock variation, and the economic conditions in the two periods are quite different (causing OVB).


4.  Use the *ssaggregate* command to run both of the previous IV regressions at the shock level. You should control for year fixed effects in the shock-level IV regressions. The coefficients should be identical to the previous estimates, but the standard errors will be different. Comment on the change.

```{stata, collectcode=T}

/* Get exposure-robust SEs with ssaggregate */
ssaggregate y x y_lag [aw=weight], ///
	n(industry) l(location) t(year) ///
	s(ind_share) sfilename(adh_shares) ///
	controls("post sum_share post_sum_share") 
merge 1:1 industry year using adh_shocks, nogen 
```

```{stata}
ivreg2 y (x=shock) i.year [aw=s_n], cluster(industry)
```

```{stata}
ivreg2 y_lag (x=shock) i.year [aw=s_n], cluster(industry)
```



